# Maintainer: Yardena Cohen <yardenack at gmail dot com>

pkgbase=s3cmd
pkgname=${pkgbase}-git
pkgver=1201.e7fad10
pkgrel=1
pkgdesc="A command line client for Amazon S3 (git)"
arch=('any')
url="https://github.com/s3tools/${pkgbase}"
license=('GPL')
depends=('python2' 'python2-dateutil')
makedepends=(git python2-setuptools)
optdepends=('gnupg: encrypted file storage')
provides=('s3cmd')
conflicts=('s3cmd')
source=("git+${url}.git")
sha512sums=('SKIP')

pkgver() {
	 cd "${srcdir}/${pkgbase}"
	 local ver="$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
	 printf "%s" "${ver//-/.}"
}

package() {
  cd "${srcdir}/${pkgbase}"
  python2 setup.py install --root="$pkgdir"
}
