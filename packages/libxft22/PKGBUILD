# Maintainer: Yardena Cohen <yardenack@gmail.com>
# Legacy Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxft22
pkgver=2.2.0
pkgrel=3
pkgdesc="FreeType-based font drawing library for X (version 2.2.0 before the bug)"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('fontconfig' 'libxrender')
makedepends=('pkgconfig')
options=('!libtool')
conflicts=('libxft')
provides=('libxft')
source=("${url}/releases/individual/lib/libXft-${pkgver}.tar.bz2"
        'freetype.patch')
sha512sums=('948ba8918895e49705b4b925b7821a3885c6b9de2e1fe1e95e3fcfee10d06906bf00eeb8035b0ae1a911c28028de0bd109fd6bda54695a3bbd2a19a80f0de37a'
            'af979bd9f3fab3b05db83abfbdbc0e1655cb1cbb741ae83a71af1084fd6e5f1cf40f21222a4278d11c1263c6f91b916e0624e2fe04a484717f5bd903b6ec7bd1')

build() {
  cd "${srcdir}/libXft-${pkgver}"
  patch -Np1 -i ${srcdir}/freetype.patch
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/libXft-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

}
