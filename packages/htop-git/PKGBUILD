gitname=htop
pkgname=${gitname}-git
pkgver=381.ff6cddf
pkgrel=1
pkgdesc="Interactive text-mode process viewer"
url="https://github.com/hishamhm/${gitname}"
license=('GPL')
arch=('i686' 'x86_64')
depends=('ncurses')
makedepends=('git' 'python2')
optdepends=('lsof: list open files for running process'
            'strace: attach to running process')
provides=('htop')
conflicts=('htop')
options=('!emptydirs')
source=("git+${url}.git" "0001-pass-processlist-flags-to-readOpenVZData-fixes-build.patch")
sha256sums=('SKIP' '6a4420bcf317710bbd36783c425986d6d01fa12365818b9342ff9648eaf84a81')

pkgver() {
	 cd "${srcdir}/${gitname}"
	 local ver="$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
	 printf "%s" "${ver//-/.}"
}

prepare() {
	 cd "${srcdir}/${gitname}"

	 patch -Np1 -i "${srcdir}/0001-pass-processlist-flags-to-readOpenVZData-fixes-build.patch"

	 ./autogen.sh

	 sed -i 's|ncursesw/curses.h|curses.h|' RichString.[ch] configure
	 sed -i 's|python|python2|' scripts/MakeHeader.py

	 ./configure \
        --prefix=/usr \
        --enable-unicode \
        --enable-openvz \
        --enable-vserver \
        --enable-cgroup \
        --enable-oom
}

build() {
	 cd "${srcdir}/${gitname}"
	 make
}

package() {
	 cd "${srcdir}/${gitname}"
	 make DESTDIR="${pkgdir}" install
}
